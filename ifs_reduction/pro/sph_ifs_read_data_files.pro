;+
; NAME:
;
;  SPH_IFS_READ_DATA_FILES
;
; PURPOSE:
;
;  Read the list of data files from the text file generated by the
;  bash part of the pipeline
;
; CALLING SEQUENCE:
;
;    files = sph_ifs_read_data_files('file_list.dat')
;
; DESCRIPTION:
;
;  This routine reads the name of the science and calibration files
;  directly from a file generated by the bash part of the IFS
;  pipeline. It avoids having to enter manually all the file names in
;  the IDL part of the code.
;
;  The result is a structure, which contains arrays of the file names
;  in the wave_file, cent_file, flux_file and coro_file fields.
;
; INPUTS:
;
;  path - path to the file that contains the file names
;
; OPTIONAL INPUTS:
;
;  files_struct - structure with the file names
;
; OUTPUTS:
;
;  Detector flat field and bad pixel map
;
; MODIFICATION HISTORY:
;
;  arthur.vigan - 07/2017
;                 First version
;                 
; AUTHOR:
; 
;   Arthur Vigan
;   Laboratoire d'Astrophysique de Marseille
;   arthur.vigan@lam.fr
;
; LICENSE:
;
;   This code is released under the MIT license.
;   
;-

function sph_ifs_read_data_files,path
  on_error,2
  
  openr,lun,path,/get_lun  
  keys   = ''
  values = ''
  line   = ''
  while not EOF(lun) do begin
     readf,lun,line
          
     tmp = strsplit(line,':',/extract,count=count)
     if (count eq 0) then continue $
     else begin
        key = tmp[0]

        pos = strpos(line, ':')
        line = "'"+strtrim(strmid(line, pos+1),2)+"'"
        val  = strjoin(strsplit(line, ',',/extract), "','")
        
        print,key,val
     endelse

     keys = [keys,key]
     values = [values,val]
  endwhile  
  free_lun,lun
  
  keys   = keys[1:*]
  nkeys  = n_elements(keys)
  values = values[1:*]
  
  str = 'files_struct = {'
  for k=0,nkeys-1 do str += keys[k]+':['+values[k]+'],'     
  str += 'tmp:!values.f_nan}'

  res = execute(str)
  
  return,files_struct
end
